; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Paperless"
#define MyAppVersion "1.0.0"
#define MyAppPublisher "My Company, Inc."
#define MyAppURL "http://www.example.com/"
#define MyAppExeName "paperless.exe"
#define MyAppFlag "penguinSeven"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{C892CF97-477A-48FD-9168-437585227F52}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={sd}\{#MyAppName}
DisableProgramGroupPage=yes
LicenseFile=E:\upupw\运行库说明.txt
; InfoBeforeFile=E:\upupw\doc\瀹瑁?txt
; InfoAfterFile=E:\upupw\运行库说明.txt
OutputDir=E:\
OutputBaseFilename=setup
; SetupIconFile=E:\upupw\htdocs\favicon.ico
Compression=lzma
SolidCompression=yes

[Languages]
Name: "china"; MessagesFile: "compiler:Languages\china.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: checkablealone
;Name: "Tasks_1" ; Description:"安装数据库 (建议勾选)"; Flags: unchecked
;Name: "Tasks_2" ; Description:"安装文件服务器配置 (建议勾选)"; Flags: unchecked

[Files]
Source: "E:\upupw\psvince.dll"; Flags: dontcopy
Source: "E:\upupw\Apache2\*"; DestDir: "{app}\Apache2\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "E:\upupw\Backup\*"; DestDir: "{app}\Backup\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "E:\upupw\doc\*"; DestDir: "{app}\doc\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "E:\upupw\FileZillaftp\*"; DestDir: "{app}\FileZillaftp\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "E:\upupw\Guard\*"; DestDir: "{app}\Guard\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "E:\upupw\htdocs\paperless\webapp\*"; DestDir: "{app}\htdocs\paperless\webapp\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "E:\upupw\PHP7\*"; DestDir: "{app}\PHP7\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "E:\upupw\Redis\*"; DestDir: "{app}\Redis\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "E:\upupw\upcore\*"; DestDir: "{app}\upcore\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "E:\upupw\xdebug\*"; DestDir: "{app}\xdebug\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "E:\upupw\paperless\ServerDaemon\*"; DestDir: "{app}\paperless\ServerDaemon\"; Flags: ignoreversion recursesubdirs createallsubdirs

Source: "E:\upupw\paperless\FileServer\FileServer.exe"; DestDir: "{app}\paperless\FileServer\"; Flags: ignoreversion;
Source: "E:\upupw\paperless\FileServer\Net.dll"; DestDir: "{app}\paperless\FileServer\"; Flags: ignoreversion;
Source: "E:\upupw\paperless\paperlessServer\libCom.dll"; DestDir: "{app}\paperless\paperlessServer\"; Flags: ignoreversion;
Source: "E:\upupw\paperless\paperlessServer\mfc90.dll"; DestDir: "{app}\paperless\paperlessServer\"; Flags: ignoreversion;
Source: "E:\upupw\paperless\paperlessServer\PaperlessServer.exe"; DestDir: "{app}\paperless\paperlessServer\"; Flags: ignoreversion;

Source: "E:\upupw\htdocs\favicon.ico"; DestDir: "{app}\htdocs\"; Flags: ignoreversion;
Source: "E:\upupw\paperless.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "E:\upupw\boot.cmd"; DestDir: "{app}"; Flags: ignoreversion
Source: "E:\upupw\paperlessCms.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "E:\upupw\start.cmd"; DestDir: "{app}"; Flags: ignoreversion
; Source: "E:\upupw\uninstall.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "E:\upupw\运行库说明.txt"; DestDir: "{app}"; Flags: ignoreversion
Source: "E:\upupw\updateSql.exe"; DestDir: "{app}"; Flags: ignoreversion

; 安装复制，更新忽略
Source: "E:\upupw\MariaDB\*"; DestDir: "{app}\MariaDB\";Flags: ignoreversion recursesubdirs createallsubdirs; Check: MyProgCheck;
Source: "E:\upupw\paperless\FileServer\etc\config.xml"; DestDir: "{app}\paperless\FileServer\etc\"; Flags: ignoreversion; Check: MyProgCheck ;
Source: "E:\upupw\paperless\paperlessServer\etc\config.xml"; DestDir: "{app}\paperless\paperlessServer\etc\"; Flags: ignoreversion; Check: MyProgCheck;

; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{commonprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Registry]
; Root: HKLM; Subkey: "SOFTWARE\Microsoft\Windows\CurrentVersion\Run"; ValueType: string; ValueName: "upupw"; ValueData: "{app}\{#MyAppExeName}"
Root: HKLM; Subkey: "SOFTWARE\Microsoft\Windows\CurrentVersion\Run"; ValueType: string; ValueName: "startServer"; ValueData: "{app}\start.cmd"
Root: HKLM; Subkey: Software\The Application; ValueType: string; ValueName: "{#MyAppFlag}"; ValueData: '2.0'; Flags: uninsdeletekey


[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent
Filename: "{app}\boot.cmd"; Flags: nowait runhidden

[UninstallRun]
; Filename: "{app}\uninstall.exe"; Flags: nowait runhidden

[Code]
function IsModuleLoaded(modulename: AnsiString ):  Boolean;
external 'IsModuleLoaded@files:psvince.dll stdcall';

var
Page: TWizardPage;
RadioButton1, RadioButton2: TRadioButton;
Lbl1, Lbl2: TNewStaticText;
TaskCode: Boolean;

// PSVince控件在64位系统（Windows 7/Server 2008/Server 2012）下无法检测到进程，使用下面的函数可以解决。
function IsAppRunning(const FileName : string): Boolean;
var
    FSWbemLocator: Variant;
    FWMIService   : Variant;
    FWbemObjectSet: Variant;
begin
    Result := false;
    try
      FSWbemLocator := CreateOleObject('WBEMScripting.SWBEMLocator');
      FWMIService := FSWbemLocator.ConnectServer('', 'root\CIMV2', '', '');
      FWbemObjectSet := FWMIService.ExecQuery(Format('SELECT Name FROM Win32_Process Where Name="%s"',[FileName]));
      Result := (FWbemObjectSet.Count > 0);
      FWbemObjectSet := Unassigned;
      FWMIService := Unassigned;
      FSWbemLocator := Unassigned;
    except
      if (IsModuleLoaded(FileName)) then
        begin
          Result := false;
        end
      else
        begin
          Result := true;
        end
      end;
end;

  // 关闭进程
function TaskKillProcessByName(const FileName : string): Boolean;
  var
  ResultCode: Integer;
begin
    Exec(ExpandConstant('taskkill.exe'), '/f /im ' + '"' + FileName + '"', '', SW_HIDE,
     ewWaitUntilTerminated, ResultCode);
end;

  // 卸载serverDaemon服务
function TaskShutDownDaemon(): Boolean;
  var
  ResultCode: Integer;
begin
     Exec(ExpandConstant('{app}\paperless\ServerDaemon\ServerDaemon.exe'), ' -d ', '', SW_SHOW,
     ewWaitUntilTerminated, ResultCode)
end;

 // 停止服务
function TaskStopService(const FileName : string): Boolean;
  var
  ResultCode: Integer;
begin
    Exec(ExpandConstant('net.exe'), ' stop ' + '"' + FileName + '"', '', SW_HIDE,
     ewWaitUntilTerminated, ResultCode);
end;

// 验证按钮
function MyProgCheck(): Boolean;
begin
  Result := RadioButton1.Checked;
end;


//事件函数，安装，并在安装前检测程序是否在运行，如果运行先结束进程
function InitializeSetup(): Boolean;
begin
  Result := true;
  if  IsAppRunning('httpd.exe') then
  begin
    if MsgBox('安装程序检测到 {#MyAppName} 正在运行！'#13''#13'单击“是”按钮关闭程序并继续安装；'#13''#13'单击“否”按钮退出安装！', mbConfirmation, MB_YESNO) = IDYES then
    begin

      TaskKillProcessByName('{#MyAppExeName}');
      TaskStopService('UPUPW_Apache');

          if  IsAppRunning('php.exe') then
          begin
              TaskKillProcessByName('php.exe');
          end;

          if  IsAppRunning('paperlessCms.exe') then
          begin
              TaskKillProcessByName('paperlessCms.exe');
          end;

          if  IsAppRunning('redis-server.exe') then
          begin
              TaskStopService('upupw_redis_a');
          end;

          if  IsAppRunning('mysqld.exe') then
          begin
              TaskStopService('UPUPW_Database_A');
          end;

          if  IsAppRunning('ServerDaemon.exe') then
          begin
              TaskStopService('ServerDaemon');
          end;
      Result:= true;
    end
    else
      Result:= false;
  end;
end;

//事件函数，卸载，与安装类似
function InitializeUninstall(): Boolean;
  begin
    Result:= true;
    if  IsAppRunning('httpd.exe') then
    begin
      if MsgBox('卸载程序检测到 {#MyAppName} 正在运行！'#13''#13'单击“是”按钮关闭程序并继续卸载；'#13''#13'单击“否”按钮退出卸载！', mbConfirmation, MB_YESNO) = IDYES then
      begin
        TaskKillProcessByName('{#MyAppExeName}');
        TaskKillProcessByName('php.exe');
        TaskKillProcessByName('paperlessCms.exe');
        TaskShutDownDaemon();
        TaskStopService('UPUPW_Apache');
        TaskStopService('upupw_redis_a');
        TaskStopService('UPUPW_Database_A');
        Result:= true;
      end
      else
        Result:= false;
    end;
  end;

// 定义安装类型选择界面
procedure CreateAddonPage;
begin
  Page := CreateCustomPage(wpInfoBefore, '选择安装类型', '请根据您的需要选择安装的类型');

  RadioButton1 := TRadioButton.Create(Page);
  RadioButton1.Left := ScaleX(45);
  RadioButton1.Top := ScaleY(40);
  RadioButton1.Width := Page.SurfaceWidth;
  RadioButton1.Height := ScaleY(17);
  RadioButton1.Caption := '标准安装';
  RadioButton1.Checked := True;
  RadioButton1.Parent := Page.Surface;

  Lbl1 := TNewStaticText.Create(Page);
  Lbl1.Left := ScaleX(55);
  Lbl1.Top := ScaleY(60);
  Lbl1.Width := ScaleX(250);
  Lbl1.Height := ScaleY(50);
  Lbl1.Caption := '按照标准模式安装软件到您的电脑，数据库将更新覆盖。';
  Lbl1.Parent := Page.Surface;

  RadioButton2 := TRadioButton.Create(Page);
  RadioButton2.Left := ScaleX(45);
  RadioButton2.Top := RadioButton1.Top + ScaleY(60);
  RadioButton2.Width := Page.SurfaceWidth;
  RadioButton2.Height := ScaleY(17);
  RadioButton2.Caption := '更新';
  RadioButton2.Checked := false;
  RadioButton2.Parent := Page.Surface;

  Lbl2 := TNewStaticText.Create(Page);
  Lbl2.Left := ScaleX(55);
  Lbl2.Top := Lbl1.Top + ScaleY(60);
  Lbl2.Width := ScaleX(250);
  Lbl2.Height := ScaleY(50);
  Lbl2.Caption := '数据库不覆盖。';
  Lbl2.Parent := Page.Surface;
end;

// 判断本程序是否已安装
function GetInstalledVersion(): Boolean;
var
    InstalledVersion: String;
begin
    InstalledVersion :='';
    RegQueryStringValue(HKLM, 'Software\The Application', '{#MyAppFlag}', InstalledVersion);

    if length(InstalledVersion) > 0 then
    begin
        result := false;
    end else begin
        result := true;
    end
end;


// 事件函数 初始化
procedure InitializeWizard();
begin
  CreateAddonPage;
end;

// 界面显示判断
function ShouldSkipPage(PageID: Integer): Boolean;
begin
  if (PageID = wpSelectTasks) and (RadioButton2.Checked) then
    Result := True
  else if (PageID = wpSelectDir)  and (RadioButton2.Checked) then
    Result := True
  else if (PageID = wpInfoBefore)  and (GetInstalledVersion) then
    Result := True
end;







